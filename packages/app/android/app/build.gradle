plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

// Fix duplicate tink classes issue (tink vs tink-android)
// https://pub.dev/packages/unifiedpush#the-build-fails-because-of-duplicate-classes
def tink = 'com.google.crypto.tink:tink-android:1.19.0'
configurations.all {
    resolutionStrategy {
        force tink
        dependencySubstitution {
            substitute module('com.google.crypto.tink:tink') using module(tink)
        }
    }
}

def keystoreProperties = new Properties()
def localProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode','1')
def flutterVersionName = localProperties.getProperty('flutter.versionName','1.0')
def flutterNDKVersion = localProperties.getProperty('flutter.ndkVersion','29.0.13113456').toString()

apply from: "../../../keychat_rust_ffi_plugin/cargokit/gradle/plugin.gradle"
cargokit {
    manifestDir = "../../../keychat_rust_ffi_plugin/rust"
    libname = "keychat_rust_ffi_plugin"
}

android {
    compileSdkVersion localProperties.getProperty('flutter.compileSdkVersion','35').toInteger()
    namespace "com.keychat.io"
    ndkVersion flutterNDKVersion
    compileOptions {
        // Ensures Java 21 compatibility.
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        // Align Kotlin with Java 21 bytecode.
        jvmTarget = '21'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.keychat.io"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion localProperties.getProperty('flutter.minSdkVersion','31').toInteger()
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        ndk {
            // abiFilters 'x86','armeabi-v7a','arm64-v8a','x86_64'
            abiFilters 'x86','x86_64','arm64-v8a'
            // abiFilters 'ARCH_X86','ARCH_ARM32','ARCH_ARM64'
        }
    }
    splits {
        abi {
            // Disable ABI splits when building AAB (App Bundle handles this automatically)
            enable gradle.startParameter.taskNames.any { it.toLowerCase().contains("bundle") } ? false : true
            reset()
            include 'arm64-v8a', 'x86_64', 'x86'
            // include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            universalApk  true
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    
    android.applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        
        variant.outputs.all { output ->
            output.versionCodeOverride = flutterVersionCode.toInteger()
            
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            
            def abiSuffix = abiName ? "-${abiName}" : "-universal"
            
            outputFileName = "Keychat-${flutterVersionName}+${flutterVersionCode}-android${abiSuffix}.apk"
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'
}